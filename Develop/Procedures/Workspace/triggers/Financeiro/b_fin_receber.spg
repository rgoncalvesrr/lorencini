/**
   Valida o conteúdo dos campos do contas a receber

	@author    Ricardo Gonçalves
	@date      10/03/2009 20:54:33
	@trigger   fin_receber BIUD

	Histórico
	-------------------------------------------------------------------------------------------------------------------
   05/10/2022  Ricardo Gonçalves
      [-] Quebra do campo de baixa em dia, mês e ano

   25/03/2021  Ricardo Gonçalves
      [-] Correçã da exclusão de lançamento do caixa

   12/08/2016  Ricardo Gonçalves
      [+] Inclusão de ratreamento da origem do lançamento
   
   23/11/2009  Ricardo Gonçalves.
      [*] Remoção do tratamento de parcelas por documento.
   
   10/11/2009  Ricardo Gonçalves.
      [*] Data da baixa passa ser preenchida com a data de vencimento real.

   05/11/2009  Ricardo Gonçalves.
      [*] Preenchimento automático da data de baixa            
 */
Create or Replace Function b_fin_receber()
Returns trigger As
$$
declare
   origem   integer := sys_origem('fin_receber');
Begin
   if tg_op = 'DELETE' then
      perform sys_flag_mark('fin_receber', old.recno);
      delete
        from fin_caixa
       where _tabela = origem
         and _recno = old.recno;
      perform sys_flag_del('fin_receber', old.recno);
      return old;
   else
      -- Totaliza a parcela
      new.total := new.valor + coalesce(new.juros,0) + coalesce(new.multa,0) - coalesce(new.descto,0);
      
      new.valor_baixado := coalesce(new.valor_baixado, 0);

      if new.baixa is not null then
         new.baixa_dia := extract(day from new.baixa);
	      new.baixa_mes := extract(month from new.baixa);
	      new.baixa_ano := extract(year from new.baixa);
      else
         new.baixa_dia := null;
	      new.baixa_mes := null;
	      new.baixa_ano := null;
      end if;

      if ((new.valor_baixado > 0) and (new.baixa is not null) and (new.cc is null)) then
         raise '[[Para que a baixa possa ser processada a conta corrente deve ser informada.]]';
      end if;

      return new;
   end if;
End;
$$
language plpgsql;
